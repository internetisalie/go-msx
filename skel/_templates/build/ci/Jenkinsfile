def REPO_NAME = '${app.name}'
def MANIFEST_NAME = '${app.deploymentgroup}-manifest'
def GIT_CREDENTIALS = 'msx-jenkins-gen-ssh-key'
def GITHUB_CREDENTIALS = 'msx-jenkins-gen-token-secret-text'
def GITHUB_APP_ID_SONARQUBE = '7'
def GITHUB_APP_NAME_SONARQUBE = 'engit-sonar-int-gen-GPK'
def SONARQUBE_CREDENTIALS = 'SONARQUBE_GPK_ACCESS_TOKEN'
def SONARQUBE_INSTALLATION = 'GPK SonarQube'
def DOCKER_CREDENTIALS = '992579b4-1041-4bd1-827c-6d507970e355'
def SLACK_BOT_CREDENTIALS = 'slack-bot-secret'
def SLACK_CHANNEL = 'go-msx-build'
def ARTIFACTORY_CREDENTIALS = 'f79d92f8-694b-4c29-b477-faeabcef86cb'
def SOURCEGRAPH_ENDPOINT = 'https://sourcegraph.infra.ciscomsx.com'
def SOURCEGRAPH_CREDENTIALS = 'sourcegraph-lsif-token'
def BUILD_FLAGS = ""
def TRUNK = 'develop'
def TRUNK_MANIFEST_FOLDER = 'Build-Stable'
def PUBLISH = false
def TOOLS = ''
def TRIGGER = ''
def TRIGGER_URL = ''
def PUBLISH_URL = 'https://engci-maven-master.cisco.com/artifactory/symphony-group/com/cisco/vms/manifest'
def FULL_VERSION = ''

pipeline {

    agent {
        label env.SLAVE_LABEL
    }

    /* Only keep the 10 most recent builds. */
    options {
        buildDiscarder(logRotator(numToKeepStr: '10', artifactNumToKeepStr: '10'))
    }

    stages {

        stage('Preparation') {
            steps {
                deleteDir()

                script {
                    assert env.BUILD_NUMBER
                    assert env.WORKSPACE

                    if (env.BRANCH_NAME) {
                        currentBuild.description = env.BRANCH_NAME + " (Manual)"
                        TRIGGER = currentBuild.description
                        TRIGGER_URL = "https://cto-github.cisco.com/NFV-BU/${REPO_NAME}/tree/" + env.BRANCH_NAME
                    } else if (env.sha1) {
                        // When called from GitHub PR Builder
                        env.BRANCH_NAME = env.sha1
                        TRIGGER = currentBuild.description
                        TRIGGER_URL = env.ghprbPullLink
                    } else if (env.GIT_BRANCH) {
                        // When called from GitHub Push Notifier
                        env.BRANCH_NAME = env.GIT_BRANCH.replaceAll("origin/", "")
                        currentBuild.description = env.BRANCH_NAME + " (Push)"
                        TRIGGER = currentBuild.description
                        TRIGGER_URL = "https://cto-github.cisco.com/NFV-BU/${REPO_NAME}/tree/" + env.BRANCH_NAME
                    }

                    BUILD_FLAGS = "--build.number=" + env.BUILD_NUMBER

                    if (env.BRANCH_NAME == TRUNK && !env.MANIFEST_FOLDER) {
                        env.MANIFEST_FOLDER = TRUNK_MANIFEST_FOLDER
                    }

                    if (env.MANIFEST_FOLDER) {
                        BUILD_FLAGS = "${BUILD_FLAGS} --manifest.folder=${env.MANIFEST_FOLDER}"
                        PUBLISH = true
                    }

                    if (env.BUILD_FLAGS) {
                        BUILD_FLAGS = "${BUILD_FLAGS} ${env.BUILD_FLAGS}"
                    }

                    TOOLS = env.WORKSPACE + "/bin"
                    sh "mkdir -p ${TOOLS}"
                }
            }
        }

        stage('Checkout') {
            steps {

                checkout([
                    $class                           : 'GitSCM',
                    branches                         : [[name: env.BRANCH_NAME ?: TRUNK]],
                    doGenerateSubmoduleConfigurations: false,
                    extensions                       : [[
                        $class: 'RelativeTargetDirectory',
                        relativeTargetDir: REPO_NAME,
                    ]],
                    userRemoteConfigs                : [[
                        credentialsId: GIT_CREDENTIALS,
                        url          : "git@cto-github.cisco.com:NFV-BU/${REPO_NAME}.git",
                        refspec:       '+refs/pull/*:refs/remotes/origin/pr/* +refs/heads/*:refs/remotes/origin/*'
                    ]]
                ])

            }
        }

        stage('Perform Build') {
            steps {
                sshagent([GIT_CREDENTIALS]) {
                    withEnv([
                        "GOPATH=${env.WORKSPACE}/go",
                        "GOPRIVATE=cto-github.cisco.com/NFV-BU",
                        "GOPROXY=https://engci-maven.cisco.com/artifactory/go/,https://proxy.golang.org,direct",
                        "PATH+GOBIN=${env.WORKSPACE}/go/bin",
                        "WORKSPACE=$WORKSPACE/$REPO_NAME",
                        "BUILD_FLAGS=$BUILD_FLAGS"
                    ]) { dir ("$WORKSPACE") {
                        sh 'git config --global url."git@cto-github.cisco.com:".insteadOf "https://cto-github.cisco.com/"'
                        sh 'make all'
                    }}
                }
            }
        }

        stage('Index') {
            steps {
                sshagent([GIT_CREDENTIALS]) {
                    withEnv([
                        "WORKSPACE=$WORKSPACE/$REPO_NAME",
                        "SRC_ENDPOINT=${SOURCEGRAPH_ENDPOINT}"
                    ]) { dir("$WORKSPACE") {
                    withCredentials([string(credentialsId: GITHUB_CREDENTIALS, variable: 'GITHUB_ACCESS_TOKEN')]) {
                    withCredentials([string(credentialsId: SOURCEGRAPH_CREDENTIALS, variable: 'SRC_ACCESS_TOKEN')]) {
                    script {
                        if (env.BRANCH_NAME == TRUNK || env.BRANCH_NAME == 'master') {
                            // Download sourcegraph go indexer
                            sh "curl -L https://github.com/sourcegraph/lsif-go/releases/download/v1.2.0/src_linux_amd64 -o ${TOOLS}/lsif-go"
                            sh "chmod +x ${TOOLS}/lsif-go"

                            // Download sourcegraph cli
                            sh "curl -L ${SOURCEGRAPH_ENDPOINT}/.api/src-cli/src_linux_amd64 -o ${TOOLS}/src"
                            sh "chmod +x ${TOOLS}/src"

                            // Index go
                            sh "${TOOLS}/lsif-go"
                            sh "${TOOLS}/src lsif upload -github-token=${GITHUB_ACCESS_TOKEN} -ignore-upload-failure"
                        }
                    }}}}}
                }
            }
        }

        stage('Static Analysis') {
            steps {
                withEnv(["WORKSPACE=$WORKSPACE/$REPO_NAME"]) { dir("$WORKSPACE") {

                    junit 'test/junit-report.xml'

                    publishCoverage adapters: [coberturaAdapter('test/cobertura-coverage.xml')], sourceFileResolver: sourceFiles('NEVER_STORE')

                    withCredentials([string(credentialsId: GITHUB_CREDENTIALS, variable: 'GITHUB_ACCESS_TOKEN')]) {
                        withSonarQubeEnv(credentialsId: SONARQUBE_CREDENTIALS, installationName: SONARQUBE_INSTALLATION) {
                            script {
                                def sonarProperties = [
                                    'userHome': env.WORKSPACE,
                                    'links.ci': env.JOB_URL,
                                    'github.oauth': env.GITHUB_ACCESS_TOKEN,
                                    'sonar.alm.github.app.id': GITHUB_APP_ID_SONARQUBE,
                                    'sonar.alm.github.app.name': GITHUB_APP_NAME_SONARQUBE,
                                ]

                                if (env.BRANCH_NAME != TRUNK && env.BRANCH_NAME != 'master') {
                                    if (env.ghprbTargetBranch) {
                                        sonarProperties['pullrequest.github.repository'] = "NFV-BU/${REPO_NAME}"
                                        sonarProperties['pullrequest.provider'] = "github"
                                        sonarProperties['pullrequest.key'] = env.ghprbPullId
                                        sonarProperties['pullrequest.branch'] = env.ghprbSourceBranch
                                        sonarProperties['pullrequest.base'] = env.ghprbTargetBranch
                                    } else {
                                        sonarProperties['branch.name'] = env.BRANCH_NAME
                                        sonarProperties['branch.target'] = TRUNK
                                    }
                                }

                                def sonarHome = tool name: 'sonarscaner', type: 'hudson.plugins.sonar.SonarRunnerInstallation'
                                def sonarCommand = "$sonarHome/bin/sonar-scanner"
                                sonarProperties.each { key, value -> sonarCommand = sonarCommand + " -Dsonar.$key=$value" }
                                sonarCommand = sonarCommand + " -Dproject.settings=build/ci/sonar-project.properties"
                                sh sonarCommand
                            }

                            // TODO: need to create webhooks manually for each of the sonar project for sonar to send status
//                             script {
//                                 timeout(time: 10, unit: 'MINUTES') {
//                                     def qg = waitForQualityGate()
//                                     if (qg.status != 'OK') {
//                                         error "Pipeline aborted due to microservices quality gate failure: ${qg.status}"
//                                     }
//                                 }
//                             }
                        }
                    }
                }}
            }
        }

        stage('Publish Artifacts') {
            steps {
                sshagent([GIT_CREDENTIALS]) {
                    withEnv([
                        "GOPATH=${env.WORKSPACE}/go",
                        "GOPRIVATE=cto-github.cisco.com/NFV-BU",
                        "GOPROXY=https://engci-maven.cisco.com/artifactory/go/,https://proxy.golang.org,direct",
                        "PATH+GOBIN=${env.WORKSPACE}/go/bin",
                        "WORKSPACE=$WORKSPACE/$REPO_NAME",
                        "BUILD_FLAGS=$BUILD_FLAGS"
                    ]) { dir ("$WORKSPACE") {
                        script { withCredentials([usernamePassword(
                            credentialsId: DOCKER_CREDENTIALS,
                            passwordVariable: 'DOCKER_PASSWORD',
                            usernameVariable: 'DOCKER_USERNAME')]) { withCredentials([usernamePassword(
                                credentialsId: ARTIFACTORY_CREDENTIALS,
                                passwordVariable: 'ARTIFACTORY_PASSWORD',
                                usernameVariable: 'ARTIFACTORY_USERNAME')]) {
                                if (PUBLISH) {
                                    sh "make publish"
                                    sh "make tag"
                                }
                        }}}
                    }}
                }
            }
        }

    }

    post {
        always {
            script {
                def RESULT_EMOJIS = [
                    "SUCCESS": ":white_check_mark:",
                    "UNSTABLE": ":jenkins_unstable:",
                    "FAILURE": ":x:",
                    "ABORTED": ":abort:"
                ]

                if (SLACK_CHANNEL != "") {
                    def RESULT_EMOJI = RESULT_EMOJIS[currentBuild.currentResult]
                    def startTimeString = new Date(currentBuild.startTimeInMillis).format("MMM dd HH:mm")
                    def durationString = currentBuild.durationString.replace(" and counting", "")

                    def section = { label, text -> ["type": "section", "fields": [ [ "type": "mrkdwn", "text": "*" + label + ":*\n" + text ] ] ] }
                    def section2 = { label1, text1, label2, text2 -> ["type": "section", "fields": [ [ "type": "mrkdwn", "text": "*" + label1 + ":*\n" + text1 ], [ "type": "mrkdwn", "text": "*" + label2 + ":*\n" + text2 ] ] ] }
                    def header = { text -> ["type": "header", "text": [ "type": "plain_text", "emoji": true, "text": text ] ] }

                    def blocks = [
                        header("${RESULT_EMOJI} ${REPO_NAME} #${env.BUILD_NUMBER}"),
                        section2(
                            "Job", "<${env.RUN_DISPLAY_URL}|${REPO_NAME}/${env.BUILD_NUMBER}>",
                            "Trigger", "<${TRIGGER_URL}|${TRIGGER}>")
                             ]

                    if (PUBLISH && currentBuild.currentResult == 'SUCCESS') {

                        def skelJson = readJSON(file:"${REPO_NAME}/.skel.json")
                        FULL_VERSION = skelJson.appVersion + "-" + env.BUILD_NUMBER
                        PUBLISH_URL = "${PUBLISH_URL}/${env.MANIFEST_FOLDER}/${MANIFEST_NAME}/${FULL_VERSION}/${MANIFEST_NAME}-${FULL_VERSION}-resources.zip"

                        blocks.add(section2(
                            "Built", "${startTimeString} - _${durationString}_",
                            "Published", "<${PUBLISH_URL}|${env.MANIFEST_FOLDER} ${FULL_VERSION}>"))
                    } else {
                        blocks.add(section(
                            "Built", "${startTimeString} - _${durationString}_"))
                    }

                    def testSummaryMicroservice = "Unknown Result"
                    try {
                        def summary = junit(
                            testResults: "${REPO_NAME}/test/junit-report.xml",
                            allowEmptyResults: true)

                        testSummaryMicroservice = "Passed: ${summary.passCount} / ${summary.totalCount}\n" +
                                                    "Failed: ${summary.failCount}\n" +
                                                    "Skipped: ${summary.skipCount}"
                    } catch (e) {}

                    blocks.add(section("Microservice Tests", testSummaryMicroservice))

                    slackSend(
                        channel: SLACK_CHANNEL,
                        blocks: blocks,
                        botUser: true,
                        tokenCredentialId: SLACK_BOT_CREDENTIALS)
                }
            }
        }
    }

}
